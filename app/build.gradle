apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

// If code style check fails do not try to make a build
apply from: 'lint.gradle'
preBuild.dependsOn ktlint

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "co.lateralview.myapp"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false

            manifestPlaceholders = [
                    crashlyticsEnabled          : false,
                    performanceMonitoringEnabled: false,
                    analyticsEnabled            : false
            ]

            buildConfigField "String", "BASE_URL", "\"DEV_URL\""
            buildConfigField "boolean", "LOG_TO_CRASHLYTICS", "false"
            buildConfigField "boolean", "MOCK_LOCATION_ALLOWED", "true"
        }
        staging {
            minifyEnabled false
            shrinkResources false

            manifestPlaceholders = [
                    crashlyticsEnabled          : true,
                    performanceMonitoringEnabled: true,
                    analyticsEnabled            : true
            ]

            buildConfigField "String", "BASE_URL", "\"STAGING_URL\""
            buildConfigField "boolean", "LOG_TO_CRASHLYTICS", "true"
            buildConfigField "boolean", "MOCK_LOCATION_ALLOWED", "true"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    crashlyticsEnabled          : true,
                    performanceMonitoringEnabled: true,
                    analyticsEnabled            : true
            ]

            buildConfigField "String", "BASE_URL", "\"PROD_URL\""
            buildConfigField "boolean", "LOG_TO_CRASHLYTICS", "true"
            buildConfigField "boolean", "MOCK_LOCATION_ALLOWED", "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions.unitTests.includeAndroidResources = true
}

ext {
    leak_canary_version = "1.6.2"
    retrofit_version = "2.4.0"
    timber_version = "4.7.1"
    threetenabp_version = "1.1.0"
    junit_version = "4.12"
    ktlint_version = "0.29.0"
    appCompat_version = "1.0.2"
    material_design_version = "1.0.0"
    constraint_layout_version = "2.0.0-alpha2"
    multidex_version = "2.0.0"
    firebase_core_version = "16.0.5"
    firebase_auth_version = "16.0.5"
    firebase_perf_version = "16.2.0"
    crashlytics_version = "2.9.6"
    rxkotlin_version = "2.3.0"
    rxandroid_version = "2.1.0"
    stetho_version = "1.5.0"
    logging_interceptor_version = "3.10.0"
    chuck_version = "1.1.0"
    dagger_version = "2.16"
    test_core_version = "1.1.0"
    mockito_core_version = "2.23.0"
    mockito_kotlin_version = "2.0.0"
    robolectric_version = "4.0.2"
    threetenbp_version = "1.3.7"
    version_play_services = "16.0.0"
    quick_permissions_version = "0.4.0"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$appCompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.multidex:multidex:$multidex_version"
    implementation "com.google.android.material:material:$material_design_version"

    //Firebase
    implementation "com.google.firebase:firebase-core:$firebase_core_version"
    implementation "com.google.firebase:firebase-perf:$firebase_perf_version"
    implementation "com.google.firebase:firebase-auth:$firebase_auth_version"
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlytics_version"

    //Google maps
    implementation "com.google.android.gms:play-services-maps:$version_play_services"
    implementation "com.google.android.gms:play-services-location:$version_play_services"
    implementation "com.github.quickpermissions:quickpermissions-kotlin:$quick_permissions_version"

    //Code style
    ktlint "com.github.shyiko:ktlint:$ktlint_version"

    //Date API
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenabp_version"

    //Leak detection
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leak_canary_version"
    stagingImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    //Logs
    implementation "com.jakewharton.timber:timber:$timber_version"

    //Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"

    //Reactive Programming
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"

    // Stetho
    implementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"

    // Friendly Http Inspector through separate App
    debugImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    stagingImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"

    //Dagger 2 - Dependency Injection
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Test
    // Unit testing framework
    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.test:core:$test_core_version"

    // Mocking framework for unit tests
    testImplementation "org.mockito:mockito-core:$mockito_core_version"

    // Library that provides helper functions to work with Mockito in Kotlin
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"

    // Unit test framework that de-fangs the Android SDK jar
    testImplementation "org.robolectric:robolectric:$robolectric_version"

    // Use no-op versions for testing
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    testImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"

    // Use threetenbp (the java version) for testing instead of threetenabp (optimized for Android)
    testImplementation("org.threeten:threetenbp:$threetenbp_version") {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
}

apply plugin: 'com.google.gms.google-services'