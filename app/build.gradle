buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

// If code style check fails do not try to make a build
apply from: 'lint.gradle'
preBuild.dependsOn ktlint

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "co.lateralview.myapp"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            dimension "default"
            buildConfigField "String", "BASE_URL", "\"DEV_URL\""
            buildConfigField "boolean", "LOGCAT_ENABLED", "true"
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", "false"
            buildConfigField "boolean", "PERFORMANCE_MONITORING_ENABLED", "false"
            buildConfigField "boolean", "ANALYTICS_ENABLED", "false"
        }

        staging {
            dimension "default"
            buildConfigField "String", "BASE_URL", "\"STAGING_URL\""
            buildConfigField "boolean", "LOGCAT_ENABLED", "true"
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", "true"
            buildConfigField "boolean", "PERFORMANCE_MONITORING_ENABLED", "true"
            buildConfigField "boolean", "ANALYTICS_ENABLED", "true"
        }

        prod {
            dimension "default"
            buildConfigField "String", "BASE_URL", "\"PROD_URL\""
            buildConfigField "boolean", "LOGCAT_ENABLED", "false"
            buildConfigField "boolean", "CRASHLYTICS_ENABLED", "true"
            buildConfigField "boolean", "PERFORMANCE_MONITORING_ENABLED", "true"
            buildConfigField "boolean", "ANALYTICS_ENABLED", "true"
        }
    }
}

ext {
    version_leak_canary = "1.5.4"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0-rc02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'com.google.android.material:material:1.0.0-rc01'

    //Firebase
    implementation "com.google.firebase:firebase-core:16.0.3"
    implementation "com.google.firebase:firebase-perf:16.1.0"
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.3@aar') {
        transitive = true
    }

    //Code style
    ktlint "com.github.shyiko:ktlint:0.28.0"

    //Date API
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'

    //Leak detection
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$version_leak_canary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$version_leak_canary"

    testImplementation 'junit:junit:4.12'
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$version_leak_canary"
}

apply plugin: 'com.google.gms.google-services'